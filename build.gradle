group 'com.github.kahalemakai'
version '0.2.1-SNAPSHOT'

buildscript {
    repositories {
        maven { url "https://plugins.gradle.org/m2/" }
        maven { url "file://${System.getProperty("user.home")}/.m2/repository" }
        jcenter()
    }
    dependencies {
        classpath "io.spring.gradle:dependency-management-plugin:0.6.0.RELEASE"
    }
}

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'jacoco'
apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: "io.spring.dependency-management"

def delombokDir = "${buildDir}/src-delomboked"

idea {
    project {
        languageLevel = '1.8'
        vcs = 'Git'
        ipr {
            withXml { provider ->
                // Get XML as groovy.util.Node to work with.
                def projectXml = provider.asNode()
                // Find compiler configuration component.
                def compilerConfiguration = projectXml.component.find { component ->
                    component.'@name' == 'CompilerConfiguration'
                }
                // Replace current annotationProcessing
                // that is part of the compiler configuration.
                def currentAnnotationProcessing = compilerConfiguration.annotationProcessing
                currentAnnotationProcessing.replaceNode {
                    annotationProcessing {
                        profile(name: 'Default', default: true, enabled: true) {
                            processorPath(useClasspath: true)
                        }
                    }
                }
            }
        }
    }
    module {
        jdkName = '1.8'
        downloadJavadoc = true
        downloadSources = false
    }
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    jcenter()
    maven { url "file://${System.getProperty("user.home")}/.m2/repository" }
    maven { url "https://plugins.gradle.org/m2/" }
    maven { url "http://nexus.pmd.local:8081/nexus/content/groups/PIA_Private/" }
    maven { url "http://nexus.pmd.local:8081/nexus/content/groups/public/" }
    maven { url "https://repository.jboss.org/nexus/content/groups/public" }
}

dependencyManagement {
    compile {
        dependencies {
            dependencySet(group: "log4j", version: "1.2.17") {
                entry "log4j"
                entry "apache-log4j-extras"
            }
        }
    }
}

dependencies {
    compileOnly "org.projectlombok:lombok:1.16.10"
    compile "log4j:log4j"
    compile "log4j:apache-log4j-extras"
    compile 'com.opencsv:opencsv:3.8'

    compile(group: 'com.intellij', name: 'annotations', version: '12.0') {
        exclude group: "com.intellij"
    }
    testCompile group: 'junit', name: 'junit', version: '4.12'
}

configurations.compile.transitive = true

task wrapper(type: Wrapper) {
    gradleVersion = '2.14'
}

task delombok << {
    description 'Delomboks the source code'
    ant.taskdef(classname: 'lombok.delombok.ant.Tasks$Delombok', classpath: configurations.compile.asPath, name: 'delombok')
    ant.mkdir(dir: delombokDir)
    ant.delombok(verbose: 'true', encoding: 'UTF-8', to: delombokDir, from: 'src/main/java')
}

task javadocDelux(type: Javadoc, group: "documentation") {
    dependsOn delombok
    source delombokDir
}

jacocoTestReport {
    additionalSourceDirs = files(delombokDir)
    setDependsOn(['delombok'])
    reports {
        xml.enabled true
    }
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadocDelux
    dependsOn javadocDelux
}

task sourcesJar(type: Jar) {
    dependsOn classes
    classifier = 'sources'
    from sourceSets.main.allSource
}

gradle.projectsEvaluated {
    tasks.withType(JavaCompile) {
        options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation" << "-Xdiags:verbose"
    }
}

task cleanTests(group: "verification") {
    dependsOn cleanTest
}

defaultTasks 'clean', 'test', 'build' //, 'jacocoTestReport'
